# Maintainer: Hec <hec@heccraft.com>
pkgname=bootc-git
_pkgname=bootc
pkgver=1.10.0.r113.g535f1e7
pkgrel=1
pkgdesc="Boot and upgrade via container images"
arch=('x86_64')
url="https://github.com/bootc-dev/$_pkgname"
license=('MIT OR Apache-2.0')
depends=(gcc-libs
  glibc
  ostree) 
makedepends=(cargo
  go-md2man
  git)
conflicts=(bootc)
provides=("bootc=$pkgver")
source=("git+$url.git")
sha256sums=('SKIP'
            'c6ab974a80242372b07891c51b3b9689570ad2aa1dca4c7386be82579ec69956')

prepare() {
  cd "$_pkgname"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

pkgver() {
  cd "$_pkgname"
  git describe --long --tags --abbrev=7 --match="v*" HEAD |
    sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$_pkgname"
  export CARGO_TARGET_DIR=target
  make bin
  cargo run --frozen --package xtask -- manpages
}

check() {
  export RUST_BACKTRACE=1
  cd $_pkgname
  cargo test --frozen
}

package() {
  cd "$_pkgname"
  install -Dm 644 "LICENSE-APACHE" "$pkgdir/usr/share/licenses/$_pkgname/LICENSE-APACHE"
  install -Dm 644 "LICENSE-MIT" "$pkgdir/usr/share/licenses/$_pkgname/LICENSE-MIT"
  make DESTDIR="$pkgdir/" install-all
}
