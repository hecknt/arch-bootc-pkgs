# Maintainer: Hec <hec@heccraft.com>

pkgname=bcvk
pkgver=0.9.0
pkgrel=1
pkgdesc='Launches ephemeral VMs from bootc containers and creates disk images.'
url='https://github.com/bootc-dev/bcvk'
license=('Apache-2.0 OR MIT')
makedepends=(
  cargo
  go-md2man
)
depends=(
  glibc
  openssh
  qemu-system-x86
  virtiofsd
  podman
)
arch=('x86_64' 'aarch64')
source=("https://github.com/bootc-dev/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.zstd")
sha256sums=('77177a73132eca6c528c2969a7f1b7619178cc4afb1101ac6665e964f348168e')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  export CARGO_TARGET_DIR=target
  cd $pkgname-$pkgver
  make bin manpages
}

check() {
  export RUST_BACKTRACE=1
  cd $pkgname-$pkgver
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 644 "LICENSE-APACHE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE-APACHE"
  install -Dm 644 "LICENSE-MIT" "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"
  make DESTDIR="$pkgdir/" install
}
