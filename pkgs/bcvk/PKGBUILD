# Maintainer: Hec <hec@heccraft.com>

pkgname=bcvk
pkgver=0.7.0
pkgrel=1
pkgdesc='Launches ephemeral VMs from bootc containers and creates disk images.'
url='https://github.com/bootc-dev/bcvk'
license=('Apache-2.0 OR MIT')
makedepends=(
  cargo
)
depends=(
  glibc
  openssh
  qemu-system-x86
  virtiofsd
  podman
)
arch=(x86_64)
source=("https://github.com/bootc-dev/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.zstd")
sha256sums=('f56012088ba93607fc55a2c3ae38e7ca0bdcea128a85da8035901cdd3802566f')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../virtiofsd-path.patch
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  export CARGO_TARGET_DIR=target
  cd $pkgname-$pkgver
  make bin
}

check() {
  export RUST_BACKTRACE=1
  cd $pkgname-$pkgver
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 644 "LICENSE-APACHE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE-APACHE"
  install -Dm 644 "LICENSE-MIT" "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"
  make DESTDIR="$pkgdir/" install
}
