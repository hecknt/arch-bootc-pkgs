# Maintainer: Hec <hec@heccraft.com>
pkgname=bootc
pkgver=1.11.0
pkgrel=1
pkgdesc="Boot and upgrade via container images"
arch=('x86_64')
url="https://github.com/bootc-dev/$pkgname"
license=('MIT OR Apache-2.0')
depends=(bash
  gcc-libs
  glib2
  glibc
  openssl
  ostree
  zlib
  zstd)
makedepends=(cargo
  go-md2man)
source=("https://github.com/bootc-dev/bootc/releases/download/v$pkgver/bootc-$pkgver.tar.zstd")
sha256sums=('700d83df65ab860026ebf386ade15820a96336f3c27a1792519354f83ffb0563')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  export CARGO_TARGET_DIR=target
  make bin
}

check() {
  export RUST_BACKTRACE=1
  cd $pkgname-$pkgver
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 644 "LICENSE-APACHE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE-APACHE"
  install -Dm 644 "LICENSE-MIT" "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"
  make DESTDIR="$pkgdir/" install-all
}
