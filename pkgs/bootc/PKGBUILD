# Maintainer: Hec <hec@heccraft.com>
pkgname=bootc
pkgver=1.10.0
pkgrel=3
pkgdesc="Boot and upgrade via container images"
arch=('x86_64')
url="https://github.com/bootc-dev/$pkgname"
license=('MIT OR Apache-2.0')
depends=(bash
  gcc-libs
  glib2
  glibc
  openssl
  ostree
  zlib
  zstd) 
makedepends=(cargo
  go-md2man)
source=("https://github.com/bootc-dev/bootc/releases/download/v$pkgver/bootc-$pkgver.tar.zstd" "pr1779.patch")
sha256sums=('39b39a9bb92680f1690e262c7190fb9cd2d5f2dec8d2904df2bbaecd594d1118'
            'c6ab974a80242372b07891c51b3b9689570ad2aa1dca4c7386be82579ec69956')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i ../pr1779.patch
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  export CARGO_TARGET_DIR=target
  make bin
}

check() {
  export RUST_BACKTRACE=1
  cd $pkgname-$pkgver
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 644 "LICENSE-APACHE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE-APACHE"
  install -Dm 644 "LICENSE-MIT" "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"
  make DESTDIR="$pkgdir/" install-all install-initramfs-dracut # note: remove install-initramfs-dracut when 1.11.0 comes out
}
