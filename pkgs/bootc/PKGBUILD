# Maintainer: Hec <hec@heccraft.com>
pkgname=bootc
pkgver=1.11.0
pkgrel=2
pkgdesc="Boot and upgrade via container images"
arch=('x86_64')
url="https://github.com/bootc-dev/$pkgname"
license=('MIT OR Apache-2.0')
depends=(bash
  gcc-libs
  glib2
  glibc
  openssl
  ostree
  zlib
  zstd)
makedepends=(cargo
  go-md2man
  git)
source=("git+$url.git" "fix-usroverlay.patch")
sha256sums=('SKIP'
            'e530bc55f3c6549db8d2655f00b65d52bf33a399a45f6dda0fa36994307d01d7')

prepare() {
  cd "$pkgname"
  git checkout v$pkgver
  patch -Np1 -i "$srcdir/fix-usroverlay.patch"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"
  export CARGO_TARGET_DIR=target
  make bin
}

check() {
  export RUST_BACKTRACE=1
  cd $pkgname
  cargo test --frozen
}

package() {
  cd "$pkgname"
  install -Dm 644 "LICENSE-APACHE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE-APACHE"
  install -Dm 644 "LICENSE-MIT" "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"
  make DESTDIR="$pkgdir/" install-all
}
